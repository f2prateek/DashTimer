buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:0.12.+'
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    classpath 'com.jakewharton.hugo:hugo-plugin:1.1.+'
  }
}

allprojects {
  repositories {
    mavenCentral()
  }
}

def _gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def _buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

ext {
  gitSha = "\"${_gitSha}\""
  buildTime = "\"${_buildTime}\""

  applicationId = "com.f2prateek.dashtimer"

  versionMajor = 0
  versionMinor = 0
  versionPatch = 1
  versionBuild = 0

  versionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
  versionName = versionMajor + "." + versionMinor + "." + versionPatch

  minSdkVersion = 19 // minSdk for library and phone app
  minSdkVersionWear = 20 // minSdk for wear app
  targetSdkVersion = 20

  compileSdkVersion = 20
  buildToolsVersion = '20.0.0'
}

ext.libraries = [dagger         : 'com.squareup.dagger:dagger:1.2.+',
                 daggerCompiler : 'com.squareup.dagger:dagger-compiler:1.2.+',
                 otto           : 'com.squareup:otto:1.3.+',
                 ln             : 'com.f2prateek.ln:ln:1.1.+',
                 butterknife    : 'com.jakewharton:butterknife:5.1.+',
                 dart           : 'com.f2prateek.dart:dart:1.1.+',
                 auto           : 'com.github.frankiesardo:android-auto-value-processor:+',
                 supportWearable: 'com.google.android.support:wearable:+',
                 playWearable   : 'com.google.android.gms:play-services-wearable:+']